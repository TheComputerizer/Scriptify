plugins {
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group
archivesBaseName = project.base_name

java.toolchain.getLanguageVersion().set(JavaLanguageVersion.of(8))

minecraft {
    mappings channel: 'stable', version: mappings_version
    accessTransformer = file('src/main/resources/META-INF/scriptify_at.cfg')
    runs {
        def args = [
                '-Dmixin.debug.export=true',
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true'
        ]
        client {
            workingDirectory file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}

repositories {
    maven {
        name = 'BlameJared'
        url = uri('https://maven.blamejared.com')
    }
    maven {
        name = 'CleanRoom'
        url = uri('https://maven.cleanroommc.com')
    }
    maven {
        name = 'CurseForge'
        url = uri('https://www.cursemaven.com')
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    //minecraft
    minecraft("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")

    //annotation processors
    annotationProcessor("com.google.code.gson:gson:${project.gson_annotation}")
    annotationProcessor("com.google.guava:guava:${project.guava_annotation}")
    annotationProcessor("org.ow2.asm:asm-debug-all:${project.asm_debug_annotation}")
    annotationProcessor("org.projectlombok:lombok:${project.lombok_version}")
    annotationProcessor("zone.rong:mixinbooter:${project.mixinbooter_version}") {
        transitive = false
    }

    //compile only stuff
    compileOnly("org.projectlombok:lombok:${project.lombok_version}")

    //implemented stuff
    implementation("zone.rong:mixinbooter:${project.mixinbooter_version}")
    implementation("CraftTweaker2:CraftTweaker2-API:${project.ct_version}")
    implementation("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${project.ct_version}")
    implementation("CraftTweaker2:CraftTweaker2-MC1120-Mod-JEI:1.12-${project.ct_version}") {
        transitive = false
    }
    implementation("CraftTweaker2:ZenScript:${project.ct_version}")
    implementation(fg.deobf("curse.maven:the-impossible-library-661115:${project.til_version}"))

    //runtime stuff and libraries
    runtimeOnly(fg.deobf("curse.maven:brandons-core-231382:${project.bc_version}"))
    runtimeOnly(fg.deobf("curse.maven:bookshelf-228525:${project.bookshelf_version}"))
    runtimeOnly(fg.deobf("curse.maven:caliper-266824:${project.caliper_version}"))
    runtimeOnly(fg.deobf("curse.maven:codechicken-lib-242818:${project.ccl_version}"))
    runtimeOnly(fg.deobf("curse.maven:cucumber-272335:${project.cucumber_version}"))
    runtimeOnly(fg.deobf("curse.maven:extended-crafting-nomifactory-edition-398267:${project.excrafting_version}"))
    runtimeOnly(fg.deobf("curse.maven:had-enough-items-557549:${project.hei_version}"))
    runtimeOnly(fg.deobf("curse.maven:modtweaker-220954:${project.modtweaker_version}"))
    runtimeOnly(fg.deobf("curse.maven:mtlib-253211:${project.mtlib_version}"))
    runtimeOnly(fg.deobf("curse.maven:redstone-flux-270789:${project.rf_version}"))
    runtimeOnly(fg.deobf("curse.maven:the-one-probe-245211:${project.top_version}"))
    runtimeOnly(fg.deobf("curse.maven:zenrecipereload-911152:${project.zenrr_version}"))
    runtimeOnly(fg.deobf("curse.maven:zenutil-401178:${project.zenutils_version}"))
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': minecraft_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "scriptify.refmap.json"
}

jar {
    manifest {
        attributes([
                /*'FMLCorePlugin': 'mods.thecomputerizer.scriptify.core.EarlyMixinPlugin',
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true,*/
                'FMLAT': 'scriptify_at.cfg'
        ])
    }
}